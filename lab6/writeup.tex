% template created by: Russell Haering. arr. Joseph Crop
\documentclass[12pt,letterpaper]{article}
\usepackage{anysize}
\marginsize{2cm}{2cm}{1cm}{1cm}

\begin{document}

\begin{titlepage}
    \vspace*{4cm}
    \begin{flushright}
    {\huge
        ECE 375 Lab 6\\[1cm]
    }
    {\large
        Remotely Operated Vehicle
    }
    \end{flushright}
    \begin{flushleft}
    Lab Time: Wednesday 5-7
    \end{flushleft}
    \begin{flushright}
    Sean Rettig
    \vfill
    \rule{5in}{.5mm}\\
    TA Signature
    \end{flushright}

\end{titlepage}

\section{Introduction}

The purpose of this sixth lab was to familiarize ourselves with the concepts
related to using USART I/O and further improve our knowledge of interrupts by
creating a 2-AVR system where one acts like a BumpBot while the other acts as a
remote that can control it.

\section{Program Overview}

\subsection{Remote}

The remote program sends commands to the robot via the USART infrared LED.  For
each command, it first sends a robot ID that is shared between the remote and
the robot.  The robot should only respond to commands preceded by the correct
ID.  The most significant bit of each ID is 0, while the most significant bit
of each command is 1, allowing the types of data to be differentiated.  Since
commands store information in the upper 4 bits of the byte, they are shifted
down so the most significant bit of each command can be a 1; the robot unshifts
the command before running it.

\subsubsection{Initialization Routine}

First, USART1 is initialized to run at 2400 baud with 8 data bits and 2 stop
bits; the robot is set the same way.  It also enables transmitting.

\subsubsection{Main Routine}

The main routine simply sends a list of predefined commands to the robot in a
loop, waiting some number of seconds in between each command.

\subsubsection{Command Routines}

There is a command routine for each of the 5 possible commands (move forward,
move backward, turn left, turn right, and halt).  Each one simply sends the
robot ID byte followed by the command byte.

\subsubsection{Wait Routine}

The Wait routine simply runs a long loop during which it does nothing in order
to "wait" a certain amount of time.

\subsection{Robot}

The robot waits until it receives a command from the remote, at which point the
command is executed.  If either whisker is hit, it will back up and turn away
in standard BumpBot fashion automatically before it will accept further
commands.

\subsubsection{Initialization Routine}

First, the I/O ports are initialized so the motors and whiskers can be used
(just like in the BumpBot program).  Then, USART1 is initialized to run at 2400
baud with 8 data bits and 2 stop bits; the remote is set the same way.  It also
enables receiving and receive interrupts.  Interrupts for the whiskers are also
set.

\subsubsection{Main Routine}

The main routine does nothing but loop infinitely, as all actions to be
performed run on interrupts.

\subsubsection{USART\_Receive Routine}

This is the real "main" routine; it runs whenever USART data has been received
and processes it.  It first checks if the data is an ID or a command; if it's
an ID, it checks if it's the correct one, and if so, goes into "accept mode",
meaning that it will run the next command given to it.  If it's given a
command, it will run it only if the robot is already in accept mode, and then
exit accept mode since it had just accepted a command; the next command
received from a remote will be preceded by its own ID byte.

\subsubsection{HitRight Routine}

The HitRight Routine is also triggered by interrupts and runs when the right
whisker is hit.  When this routine runs, it ignores any remote commands given
to it while it moves backward for 1 second and turns left for 1 second.  It
will then continue whatever it what doing before the routine was triggered.  It
accomplishes the timed events by setting movement signals to PORTB and then
calling the Wait routine to wait a short amount of time before setting a new
movement signal.

\subsubsection{HitLeft Routine}

The HitLeft Routine is also triggered by interrupts and runs when the left
whisker is hit.  When this routine runs, it ignores any remote commands given
to it while it moves backward for 1 second and turns right for 1 second.  It
will then continue whatever it what doing before the routine was triggered.  It
accomplishes the timed events by setting movement signals to PORTB and then
calling the Wait routine to wait a short amount of time before setting a new
movement signal.

\subsubsection{Wait Routine}

The Wait routine simply runs a long loop during which it does nothing in order
to "wait" a certain amount of time.

\section{Conclusion}

In this lab, I learned about how to initialize and use USART for both input and
output, as well as trigger interrupts on USART events.  While the code for this
lab now seems fairly simple, getting it working for the first time was very
confusing as there is a lot that can go wrong, given how much configuration is
needed to get USART, the interrupts, and the ports working not only
individually, but also between the AVRs.

\section{Source Code}

\subsection{Remote}

\begin{verbatim}
.include "m128def.inc"          ; Include definition file

;***********************************************************
;*  Internal Register Definitions and Constants
;***********************************************************
.def    mpr = r16               ; Multi-Purpose Register
.def    waitcnt = r17           ; Wait Loop Counter
.def    ilcnt = r18             ; Inner Loop Counter
.def    olcnt = r19             ; Outer Loop Counter

.equ    WTime = 100             ; Time to wait in wait loop

.equ    WskrR = 0               ; Right Whisker Input Bit
.equ    WskrL = 1               ; Left Whisker Input Bit
.equ    EngEnR = 4              ; Right Engine Enable Bit
.equ    EngEnL = 7              ; Left Engine Enable Bit
.equ    EngDirR = 5             ; Right Engine Direction Bit
.equ    EngDirL = 6             ; Left Engine Direction Bit

.equ    BotID = 0b00100101      ; 37; Unique XD ID (MSB = 0)

; Use these commands between the remote and TekBot
; MSB = 1 thus:
; commands are shifted right by one and ORed with 0b10000000 = $80
.equ    MovFwd =  ($80|1<<(EngDirR-1)|1<<(EngDirL-1))   ;0b10110000 Move Forwards Command
.equ    MovBck =  ($80|$00)                             ;0b10000000 Move Backwards Command
.equ    TurnR =   ($80|1<<(EngDirL-1))                  ;0b10100000 Turn Right Command
.equ    TurnL =   ($80|1<<(EngDirR-1))                  ;0b10010000 Turn Left Command
.equ    Halt =    ($80|1<<(EngEnR-1)|1<<(EngEnL-1))     ;0b11001000 Halt Command

;***********************************************************
;*  Start of Code Segment
;***********************************************************
.cseg                           ; Beginning of code segment

;-----------------------------------------------------------
; Interrupt Vectors
;-----------------------------------------------------------
.org    $0000                   ; Beginning of IVs
        rjmp    INIT            ; Reset interrupt
.org    $0046                   ; End of Interrupt Vectors

;-----------------------------------------------------------
; Program Initialization
;-----------------------------------------------------------
INIT:
    ; Initialize stack
        LDI     mpr, high(RAMEND)
        OUT     SPH, mpr
        LDI     mpr, low(RAMEND)
        OUT     SPL, mpr

    ; Initialize I/O Ports
        ; NOTES:
        ; Port D bit 2 corresponds to INT2 and is used for receiving data on USART1
        ; Port D bit 3 corresponds to INT3 and is used for transmitting data on USART1

        ; Set Port D pin 3 (TXD1) for output
        ldi mpr, (1<<PD3)
        out DDRD, mpr

    ; Initialize USART1
        ldi     mpr, (1<<U2X1) ; Set double data rate
        sts     UCSR1A, mpr

        ; Set baud rate at 2400bps (taking into account double data rate)
        ldi     mpr, high(832) ; Load high byte of 0x0340
        sts     UBRR1H, mpr ; UBRR1H in extended I/O space
        ldi     mpr, low(832) ; Load low byte of 0x0340
        sts     UBRR1L, mpr

        ; Set frame format: 8 data, 2 stop bits
        ldi     mpr, (0<<UMSEL1 | 1<<USBS1 | 1<<UCSZ11 | 1<<UCSZ10)
        sts     UCSR1C, mpr ; UCSR0C in extended I/O space

        ; Enable transmitter
        ldi     mpr, (1<<TXEN1)
        sts     UCSR1B, mpr

;-----------------------------------------------------------
; Main Program
;-----------------------------------------------------------
MAIN:

        rcall   ButtonMovFwd
        rcall   Wait
        rcall   Wait
        rcall   ButtonTurnR
        rcall   Wait

        rjmp    MAIN

;***********************************************************
;*  Functions and Subroutines
;***********************************************************

ButtonMovFwd:
        ldi     mpr, BotID
        sts     UDR1, mpr
        ldi     mpr, MovFwd
        sts     UDR1, mpr
        ret

ButtonMovBck:
        ldi     mpr, BotID
        sts     UDR1, mpr
        ldi     mpr, MovBck
        sts     UDR1, mpr
        ret

ButtonTurnR:
        ldi     mpr, BotID
        sts     UDR1, mpr
        ldi     mpr, TurnR
        sts     UDR1, mpr
        ret

ButtonTurnL:
        ldi     mpr, BotID
        sts     UDR1, mpr
        ldi     mpr, TurnL
        sts     UDR1, mpr
        ret

ButtonHalt:
        ldi     mpr, BotID
        sts     UDR1, mpr
        ldi     mpr, Halt
        sts     UDR1, mpr
        ret

;----------------------------------------------------------------
; Sub:  Wait
; Desc: A wait loop that is 16 + 159975*waitcnt cycles or roughly 
;       waitcnt*10ms.  Just initialize wait for the specific amount 
;       of time in 10ms intervals. Here is the general eqaution
;       for the number of clock cycles in the wait loop:
;           ((3 * ilcnt + 3) * olcnt + 3) * waitcnt + 13 + call
;----------------------------------------------------------------
Wait:
        ldi     waitcnt, WTime  ; Wait for 1 second

        push    waitcnt         ; Save wait register
        push    ilcnt           ; Save ilcnt register
        push    olcnt           ; Save olcnt register

Loop:   ldi     olcnt, 224      ; load olcnt register
OLoop:  ldi     ilcnt, 237      ; load ilcnt register
ILoop:  dec     ilcnt           ; decrement ilcnt
        brne    ILoop           ; Continue Inner Loop
        dec     olcnt       ; decrement olcnt
        brne    OLoop           ; Continue Outer Loop
        dec     waitcnt     ; Decrement wait 
        brne    Loop            ; Continue Wait loop    

        pop     olcnt       ; Restore olcnt register
        pop     ilcnt       ; Restore ilcnt register
        pop     waitcnt     ; Restore wait register
        ret             ; Return from subroutine
\end{verbatim}

\subsection{Robot}

\begin{verbatim}
.include "m128def.inc"          ; Include definition file

;***********************************************************
;*  Internal Register Definitions and Constants
;***********************************************************
.def    mpr = r16               ; Multi-Purpose Register
.def    waitcnt = r17           ; Wait Loop Counter
.def    ilcnt = r18             ; Inner Loop Counter
.def    olcnt = r19             ; Outer Loop Counter
.def    data = r20
.def    command = r21
.def    accept = r22

.equ    WTime = 100             ; Time to wait in wait loop

.equ    WskrR = 0               ; Right Whisker Input Bit
.equ    WskrL = 1               ; Left Whisker Input Bit
.equ    EngEnR = 4              ; Right Engine Enable Bit
.equ    EngEnL = 7              ; Left Engine Enable Bit
.equ    EngDirR = 5             ; Right Engine Direction Bit
.equ    EngDirL = 6             ; Left Engine Direction Bit

.equ    BotID = 0b00100101      ; 37; Unique XD ID (MSB = 0)

;/////////////////////////////////////////////////////////////
;These macros are the values to make the TekBot Move.
;/////////////////////////////////////////////////////////////

.equ    MovFwd =  (1<<EngDirR|1<<EngDirL)   ;0b01100000 Move Forwards Command
.equ    MovBck =  $00                       ;0b00000000 Move Backwards Command
.equ    TurnR =   (1<<EngDirL)              ;0b01000000 Turn Right Command
.equ    TurnL =   (1<<EngDirR)              ;0b00100000 Turn Left Command
.equ    Halt =    (1<<EngEnR|1<<EngEnL)     ;0b10010000 Halt Command

;***********************************************************
;*  Start of Code Segment
;***********************************************************
.cseg                           ; Beginning of code segment

;-----------------------------------------------------------
; Interrupt Vectors
;-----------------------------------------------------------
.org    $0000                   ; Beginning of IVs
        rjmp    INIT            ; Reset interrupt
.org    $0002
        rcall   HitRight
        reti
.org    $0004
        rcall   HitLeft
        reti
.org    $003C
        rcall   USART_Receive
        reti

.org    $0046                   ; End of Interrupt Vectors

;-----------------------------------------------------------
; Program Initialization
;-----------------------------------------------------------
INIT:
    ; Initialize registers
        clr     accept
        clr     command

    ; Initialize stack
        LDI     mpr, high(RAMEND)
        OUT     SPH, mpr
        LDI     mpr, low(RAMEND)
        OUT     SPL, mpr

    ; Initialize I/O Ports
        ; NOTES:
        ; Port D bit 2 corresponds to INT2 and is used for receiving data on USART1
        ; Port D bit 3 corresponds to INT3 and is used for transmitting data on USART1

        ; Set Port D pin 2 (TXD0) for input and pins 1-0 for whisker input
        ldi mpr, (1<<PD2)|(0<<WskrR)|(0<<WskrL)
        out DDRD, mpr

        ; Set pullup resistors
        ldi mpr, (1<<PD2)|(0<<WskrR)|(0<<WskrL)
        out PORTD, mpr

        ; Initialize Port B for output
        ldi mpr, (1<<EngEnL)|(1<<EngEnR)|(1<<EngDirR)|(1<<EngDirL)
        out DDRB, mpr ; Set the DDR register for Port B
        ldi mpr, $00
        out PORTB, mpr ; Set the default output for Port B

    ; Initialize USART1
        ldi     mpr, (1<<U2X1) ; Set double data rate
        sts     UCSR1A, mpr

        ; Set baud rate at 2400bps (taking into account double data rate)
        ldi     mpr, high(832) ; Load high byte of 0x0340
        sts     UBRR1H, mpr ; UBRR1H in extended I/O space
        ldi     mpr, low(832) ; Load low byte of 0x0340
        sts     UBRR1L, mpr

        ; Set frame format: 8 data, 2 stop bits
        ldi     mpr, (0<<UMSEL1 | 1<<USBS1 | 1<<UCSZ11 | 1<<UCSZ10)
        sts     UCSR1C, mpr ; UCSR0C in extended I/O space

        ; Enable receiver and receive interrupts
        ldi     mpr, (1<<RXEN1 | 1<<RXCIE1)
        sts     UCSR1B, mpr

    ; Initialize external interrupts
        ; Set the Interrupt Sense Control to level low
        ldi mpr, (0<<ISC21)|(0<<ISC20)|(0<<ISC11)|(0<<ISC10)|(0<<ISC01)|(0<<ISC00)
        sts EICRA, mpr ; Use sts, EICRA in extended I/O space
        ; Set the External Interrupt Mask
        ldi mpr, (1<<INT2)|(1<<INT1)|(1<<INT0)
        out EIMSK, mpr
        ; Turn on interrupts
        sei

;-----------------------------------------------------------
; Main Program
;-----------------------------------------------------------
MAIN:
        rjmp    MAIN

;***********************************************************
;*  Functions and Subroutines
;***********************************************************

USART_Receive:
        push    mpr

        lds     data, UDR1 ; Read data from Receive Data Buffer

        ; Check if it's an ID or a command
        ldi     mpr, 0b10000000
        and     mpr, data
        breq    ProcessID ; If the result is all zeroes, it starts with a 0 and is an ID
        rjmp    ProcessCommand; Otherwise, it's a command

ProcessID:
        cpi     data, BotID
        breq    SetAccept ; If the BotID matches, go into accept mode
        clr     accept ; If it doesn't match, make sure we're NOT in accept mode
        rjmp    USART_Receive_End

SetAccept:
        ldi     accept, 1 ; If it does match, go into accept mode
        rjmp    USART_Receive_End

ProcessCommand:
        cpi     accept, 1
        brne    USART_Receive_End ; If we aren't in accept mode, do nothing
        ; If we are in accept mode, run the command
        lsl     data ; Decode command
        out     PORTB, data  ; Send command to port
        mov     command, data ; Save what command we're running in case a whisker interrupt needs to go back to it
        clr     accept ; Leave accept mode since we just accepted

USART_Receive_End:

        pop     mpr
        ret

;----------------------------------------------------------------
; Sub:  HitRight
; Desc: Handles functionality of the TekBot when the right whisker
;       is triggered.
;----------------------------------------------------------------
HitRight:
        push    mpr         ; Save mpr register
        push    waitcnt         ; Save wait register
        in      mpr, SREG   ; Save program state
        push    mpr         ;

        ; Move Backwards for a second
        ldi     mpr, MovBck ; Load Move Backwards command
        out     PORTB, mpr  ; Send command to port
        ldi     waitcnt, WTime  ; Wait for 1 second
        rcall   Wait            ; Call wait function

        ; Turn left for a second
        ldi     mpr, TurnL  ; Load Turn Left Command
        out     PORTB, mpr  ; Send command to port
        ldi     waitcnt, WTime  ; Wait for 1 second
        rcall   Wait            ; Call wait function

        ; Resume previous command
        out     PORTB, command

        pop     mpr     ; Restore program state
        out     SREG, mpr   ;
        pop     waitcnt     ; Restore wait register
        pop     mpr     ; Restore mpr
        ret             ; Return from subroutine

;----------------------------------------------------------------
; Sub:  HitLeft
; Desc: Handles functionality of the TekBot when the left whisker
;       is triggered.
;----------------------------------------------------------------
HitLeft:
        push    mpr         ; Save mpr register
        push    waitcnt         ; Save wait register
        in      mpr, SREG   ; Save program state
        push    mpr         ;

        ; Move Backwards for a second
        ldi     mpr, MovBck ; Load Move Backwards command
        out     PORTB, mpr  ; Send command to port
        ldi     waitcnt, WTime  ; Wait for 1 second
        rcall   Wait            ; Call wait function

        ; Turn right for a second
        ldi     mpr, TurnR  ; Load Turn Left Command
        out     PORTB, mpr  ; Send command to port
        ldi     waitcnt, WTime  ; Wait for 1 second
        rcall   Wait            ; Call wait function

        ; Resume previous command
        out     PORTB, command

        pop     mpr     ; Restore program state
        out     SREG, mpr   ;
        pop     waitcnt     ; Restore wait register
        pop     mpr     ; Restore mpr
        ret             ; Return from subroutine

;----------------------------------------------------------------
; Sub:  Wait
; Desc: A wait loop that is 16 + 159975*waitcnt cycles or roughly 
;       waitcnt*10ms.  Just initialize wait for the specific amount 
;       of time in 10ms intervals. Here is the general eqaution
;       for the number of clock cycles in the wait loop:
;           ((3 * ilcnt + 3) * olcnt + 3) * waitcnt + 13 + call
;----------------------------------------------------------------
Wait:
        push    waitcnt         ; Save wait register
        push    ilcnt           ; Save ilcnt register
        push    olcnt           ; Save olcnt register

Loop:   ldi     olcnt, 224      ; load olcnt register
OLoop:  ldi     ilcnt, 237      ; load ilcnt register
ILoop:  dec     ilcnt           ; decrement ilcnt
        brne    ILoop           ; Continue Inner Loop
        dec     olcnt       ; decrement olcnt
        brne    OLoop           ; Continue Outer Loop
        dec     waitcnt     ; Decrement wait 
        brne    Loop            ; Continue Wait loop    

        pop     olcnt       ; Restore olcnt register
        pop     ilcnt       ; Restore ilcnt register
        pop     waitcnt     ; Restore wait register
        ret             ; Return from subroutine
\end{verbatim}

\end{document}

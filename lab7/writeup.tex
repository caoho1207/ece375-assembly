% template created by: Russell Haering. arr. Joseph Crop
\documentclass[12pt,letterpaper]{article}
\usepackage{anysize}
\marginsize{2cm}{2cm}{1cm}{1cm}

\begin{document}

\begin{titlepage}
    \vspace*{4cm}
    \begin{flushright}
    {\huge
        ECE 375 Lab 7\\[1cm]
    }
    {\large
        Timers/Counters
    }
    \end{flushright}
    \begin{flushleft}
    Lab Time: Wednesday 5-7
    \end{flushleft}
    \begin{flushright}
    Sean Rettig
    \vfill
    \rule{5in}{.5mm}\\
    TA Signature
    \end{flushright}

\end{titlepage}

\section{Introduction}

The purpose of this sixth lab was to extend our remote-controlled robot system
from the previous lab by learning how to use timer/counters both for waiting
and adjusting motor speed (or LED brightness).

\section{Program Overview}

\subsection{Remote}

The remote program sends commands to the robot via the USART infrared LED.  For
each command, it first sends a robot ID that is shared between the remote and
the robot.  The robot should only respond to commands preceded by the correct
ID.  The most significant bit of each ID is 0, while the most significant bit
of each command is 1, allowing the types of data to be differentiated.  Since
commands store information in the upper 4 bits of the byte, they are shifted
down so the most significant bit of each command can be a 1; the robot unshifts
the command before running it.

\subsubsection{Initialization Routine}

An interrrupt is set for the debouncing functionality.  Then the stack is
initialized, and the I/O ports are set up; button input, LED output to confirm
button presses, and USART1 output.  Then, USART1 is initialized to run at 2400
baud with 8 data bits and 2 stop bits; the robot is set the same way.  It also
enables transmitting.  The debouncer initialization function is run, and global
interrupts are enabled.

\subsubsection{Main Routine}

The main routine loops over itself, each time checking the button input from
the debouncer to see if a button was pressed, and then running the relevant
command subroutine and resetting the relevant bit in the debouncer result.

\subsubsection{Command Routines}

There is a command routine for each of the 6 possible commands (move forward,
move backward, turn left, turn right, speed up, and slow down).  Each one
simply sends the robot ID byte followed by the command byte.

\subsection{Robot}

The robot waits until it receives a command from the remote, at which point the
command is executed.  If either whisker is hit, it will back up and turn away
in standard BumpBot fashion automatically before it will accept further
commands.

\subsubsection{Initialization Routine}

First, interrupt vectors are set for whisker input and USART input.  The stack
is initialized, and the I/O ports are initialized so the motors and whiskers
can be used (just like in the BumpBot program), as well as USART.  The timers
are then initialized; timers 0 and 2 for controlling the speed of the right and
left motors, and timer 1 for the wait subroutine.  Then, USART1 is initialized
to run at 2400 baud with 8 data bits and 2 stop bits; the remote is set the
same way.  It also enables receiving and receive interrupts.  Interrupts for
the whiskers are also set.

\subsubsection{Main Routine}

The main routine does nothing but loop infinitely, as all actions to be
performed run on interrupts.

\subsubsection{USART\_Receive Routine}

This is the real "main" routine; it runs whenever USART data has been received
and processes it.  It first checks if the data is an ID or a command; if it's
an ID, it checks if it's the correct one, and if so, goes into "accept mode",
meaning that it will run the next command given to it.  If it's given a
command, it will run it only if the robot is already in accept mode, and then
exit accept mode since it had just accepted a command; the next command
received from a remote will be preceded by its own ID byte.  It also includes
logic for ignoring speed up or slow down commands if they would move the speed
outside the range of 0-15, and calls the UpdateTimers subroutine when speed
changes to modify the motor output.

\subsubsection{UpdateTimers Routine}

This routine uses the speed value (0-15) and extrapolates it out to the 0-255
range to be set as the max for timers 0 and 2, causing the motors to change
speed in small increments.

\subsubsection{HitRight Routine}

The HitRight Routine is also triggered by interrupts and runs when the right
whisker is hit.  When this routine runs, it ignores any remote commands given
to it while it moves backward for 1 second and turns left for 1 second.  It
will then continue whatever it what doing before the routine was triggered.  It
accomplishes the timed events by setting movement signals to PORTB and then
calling the Wait routine to wait a short amount of time before setting a new
movement signal.

\subsubsection{HitLeft Routine}

The HitLeft Routine is also triggered by interrupts and runs when the left
whisker is hit.  When this routine runs, it ignores any remote commands given
to it while it moves backward for 1 second and turns right for 1 second.  It
will then continue whatever it what doing before the routine was triggered.  It
accomplishes the timed events by setting movement signals to PORTB and then
calling the Wait routine to wait a short amount of time before setting a new
movement signal.

\subsubsection{Wait Routine}

The Wait routine uses timer/counter 1 to count until 1 second has passed, after
which it returns.

\section{Additional Questions}
\begin{enumerate}

    \item In this lab, you used the Fast PWM mode of both 8-bit timers to
        toggle Pins 7 and 4 of Port B, which is one of many possible ways to
        implement variable speed on a TekBot. Imagine instead that you used
        just one of the 8-bit timers in Normal mode, and every time it
        overflowed, it generated an interrupt.  In that ISR, you manually
        toggled pins 7 and 4 of Port B. What are the advantages and
        disadvantages to this new approach, compared to the original PWM
        approach?

        The primary advantage of this approach is that only one timer is
        required to control the speed of the motors, freeing up the second
        timer for other uses.  However, doing this makes it harder to run the
        motors at different speeds, since they're bothing running off of the
        same timer.

\end{enumerate}

\section{Conclusion}

In this lab, I became familiar with timers, both for waiting/counting and for
generating waveforms that can be used to alter the speed of a motor or the
brightness of an LED.  I also learned a bit about debouncing and polling for
button input, which was especially useful because I wasn't able to get it
working properly for the previous lab (since I was trying to use interrupts).

\section{Source Code}

\subsection{Remote}

\begin{verbatim}
.include "m128def.inc"          ; Include definition file

;***********************************************************
;*  Internal Register Definitions and Constants
;***********************************************************
.def    mpr = r16               ; Multi-Purpose Register
.def    input = r20             ; Store DebounceResult

.equ    WTime = 100             ; Time to wait in wait loop

.equ    WskrR = 0               ; Right Whisker Input Bit
.equ    WskrL = 1               ; Left Whisker Input Bit
.equ    EngEnR = 4              ; Right Engine Enable Bit
.equ    EngEnL = 7              ; Left Engine Enable Bit
.equ    EngDirR = 5             ; Right Engine Direction Bit
.equ    EngDirL = 6             ; Left Engine Direction Bit

.equ    BotID = 0b00100101      ; 37; Unique XD ID (MSB = 0)

; Use these commands between the remote and TekBot
; MSB = 1 thus:
; commands are shifted right by one and ORed with 0b10000000 = $80
.equ    MovFwd =  ($80|1<<(EngDirR-1)|1<<(EngDirL-1))   ;0b10110000 Move Forwards Command
.equ    MovBck =  ($80|$00)                             ;0b10000000 Move Backwards Command
.equ    TurnR =   ($80|1<<(EngDirL-1))                  ;0b10100000 Turn Right Command
.equ    TurnL =   ($80|1<<(EngDirR-1))                  ;0b10010000 Turn Left Command
.equ    Up =      0b10000111                            ;0b10000111 Speed Up Command
.equ    Down =    0b10000110                            ;0b10000110 Speed Down Command

;***********************************************************
;*  Start of Code Segment
;***********************************************************
.cseg                           ; Beginning of code segment

;-----------------------------------------------------------
; Interrupt Vectors
;-----------------------------------------------------------
.org    $0000                   ; Beginning of IVs
        rjmp    INIT            ; Reset interrupt
.org    $001C                   ; TIMER1 OVF interrupt vector
        rcall DEBOUNCE_D        ; subroutine call to DEBOUNCE_D
        reti
.org    $0046                   ; End of Interrupt Vectors

;-----------------------------------------------------------
; Program Initialization
;-----------------------------------------------------------
INIT:
    ; Initialize stack
        LDI     mpr, high(RAMEND)
        OUT     SPH, mpr
        LDI     mpr, low(RAMEND)
        OUT     SPL, mpr

    ; Initialize I/O Ports
        ; NOTES:
        ; Port D bit 2 corresponds to INT2 and is used for receiving data on USART1
        ; Port D bit 3 corresponds to INT3 and is used for transmitting data on USART1

        ; Initialize Port B pins 7:4 and 1:0 for LED output (to confirm button presses)
        ldi     mpr, 0b11111111 ; Set Data Directional Register
        out     DDRB, mpr

        ; Initialize Port D pins 7:4 and 1:0 for button inputs and pin 3 (TXD1) for USART1 output
        ldi     mpr, 0b00001000 ; Set Data Directional Register
        out     DDRD, mpr
        ldi     mpr, 0b11110011 ; Set pullup resistors
        out     PORTD, mpr

    ; Initialize USART1
        ldi     mpr, (1<<U2X1) ; Set double data rate
        sts     UCSR1A, mpr

        ; Set baud rate at 2400bps (taking into account double data rate)
        ldi     mpr, high(832) ; Load high byte of 0x0340
        sts     UBRR1H, mpr ; UBRR1H in extended I/O space
        ldi     mpr, low(832) ; Load low byte of 0x0340
        sts     UBRR1L, mpr

        ; Set frame format: 8 data, 2 stop bits
        ldi     mpr, (0<<UMSEL1 | 1<<USBS1 | 1<<UCSZ11 | 1<<UCSZ10)
        sts     UCSR1C, mpr ; UCSR0C in extended I/O space

        ; Enable transmitter
        ldi     mpr, (1<<TXEN1)
        sts     UCSR1B, mpr
        
    ; Initialize debouncing code
        rcall DEBOUNCE_INIT

    ; Enable global interrupts
        sei

;-----------------------------------------------------------
; Main Program
;-----------------------------------------------------------
MAIN:
        ; read debounced Port D state
        lds     input, DebounceResult

CheckMovFwd:
        mov     mpr, input
        andi    mpr, (1<<7)
        brne    CheckMovBck
        rcall   ButtonMovFwd
        ori     input, (1<<7)
        sts     DebounceResult, input

CheckMovBck:
        mov     mpr, input
        andi    mpr, (1<<6)
        brne    CheckTurnR
        rcall   ButtonMovBck
        ori     input, (1<<6)
        sts     DebounceResult, input

CheckTurnR:
        mov     mpr, input
        andi    mpr, (1<<5)
        brne    CheckTurnL
        rcall   ButtonTurnR
        ori     input, (1<<5)
        sts     DebounceResult, input

CheckTurnL:
        mov     mpr, input
        andi    mpr, (1<<4)
        brne    CheckUp
        rcall   ButtonTurnL
        ori     input, (1<<4)
        sts     DebounceResult, input

CheckUp:
        mov     mpr, input
        andi    mpr, (1<<1)
        brne    CheckDown
        rcall   ButtonUp
        ori     input, (1<<1)
        sts     DebounceResult, input
CheckDown:
        mov     mpr, input
        andi    mpr, (1<<0)
        brne    DoneChecking
        rcall   ButtonDown
        ori     input, (1<<0)
        sts     DebounceResult, input

DoneChecking:
        rjmp    MAIN

;***********************************************************
;*  Functions and Subroutines
;***********************************************************

ButtonMovFwd:
        ; Set button LED
        ldi     mpr, (1<<7)
        out     PORTB, mpr

        ; Send the BotID
        ldi     mpr, BotID
        sts     UDR1, mpr

        ; Send the command
        ldi     mpr, MovFwd
        sts     UDR1, mpr

        ret

ButtonMovBck:
        ; Set button LED
        ldi     mpr, (1<<6)
        out     PORTB, mpr

        ; Send the BotID
        ldi     mpr, BotID
        sts     UDR1, mpr

        ; Send the command
        ldi     mpr, MovBck
        sts     UDR1, mpr

        ret

ButtonTurnR:
        ; Set button LED
        ldi     mpr, (1<<5)
        out     PORTB, mpr

        ; Send the BotID
        ldi     mpr, BotID
        sts     UDR1, mpr

        ; Send the command
        ldi     mpr, TurnR
        sts     UDR1, mpr

        ret

ButtonTurnL:
        ; Set button LED
        ldi     mpr, (1<<4)
        out     PORTB, mpr

        ; Send the BotID
        ldi     mpr, BotID
        sts     UDR1, mpr

        ; Send the command
        ldi     mpr, TurnL
        sts     UDR1, mpr

        ret

ButtonUp:
        ; Set button LED
        ldi     mpr, (1<<1)
        out     PORTB, mpr

        ; Send the BotID
        ldi     mpr, BotID
        sts     UDR1, mpr

        ; Send the command
        ldi     mpr, Up
        sts     UDR1, mpr

        ret
        
ButtonDown:
        ; Set button LED
        ldi     mpr, (1<<0)
        out     PORTB, mpr

        ; Send the BotID
        ldi     mpr, BotID
        sts     UDR1, mpr

        ; Send the command
        ldi     mpr, Down
        sts     UDR1, mpr

        ret

;***********************************************************
;*  Additional Program Includes
;***********************************************************
.include    "debounce.asm"
\end{verbatim}

\subsection{Robot}

\begin{verbatim}
.include "m128def.inc"          ; Include definition file

;***********************************************************
;*  Internal Register Definitions and Constants
;***********************************************************
.def    mpr = r16               ; Multi-Purpose Register
.def    B = r17         ; Wait Loop Counter
.def    data = r20
.def    command = r21
.def    accept = r22
.def    speed = r23

.equ    WTime = 100             ; Time to wait in wait loop

.equ    WskrR = 0               ; Right Whisker Input Bit
.equ    WskrL = 1               ; Left Whisker Input Bit
.equ    EngEnR = 4              ; Right Engine Enable Bit
.equ    EngEnL = 7              ; Left Engine Enable Bit
.equ    EngDirR = 5             ; Right Engine Direction Bit
.equ    EngDirL = 6             ; Left Engine Direction Bit

.equ    BotID = 0b00100101      ; 37; Unique XD ID (MSB = 0)

;/////////////////////////////////////////////////////////////
;These macros are the values to make the TekBot Move.
;/////////////////////////////////////////////////////////////

.equ    MovFwd =  (1<<EngDirR|1<<EngDirL)   ;0b01100000 Move Forwards Command
.equ    MovBck =  $00                       ;0b00000000 Move Backwards Command
.equ    TurnR =   (1<<EngDirL)              ;0b01000000 Turn Right Command
.equ    TurnL =   (1<<EngDirR)              ;0b00100000 Turn Left Command
.equ    Up =      0b00001110                ;0b11100000 Speed Up Command
.equ    Down =    0b00001100                ;0b11000000 Speed Down Command

;***********************************************************
;*  Start of Code Segment
;***********************************************************
.cseg                           ; Beginning of code segment

;-----------------------------------------------------------
; Interrupt Vectors
;-----------------------------------------------------------
.org    $0000                   ; Beginning of IVs
        rjmp    INIT            ; Reset interrupt
.org    $0002
        rcall   HitRight
        reti
.org    $0004
        rcall   HitLeft
        reti
.org    $003C
        rcall   USART_Receive
        reti

.org    $0046                   ; End of Interrupt Vectors

;-----------------------------------------------------------
; Program Initialization
;-----------------------------------------------------------
INIT:
    ; Initialize registers
        clr     accept
        clr     command
        ldi     speed, 8

    ; Initialize stack
        LDI     mpr, high(RAMEND)
        OUT     SPH, mpr
        LDI     mpr, low(RAMEND)
        OUT     SPL, mpr

    ; Initialize I/O Ports
        ; NOTES:
        ; Port D bit 2 corresponds to INT2 and is used for receiving data on USART1
        ; Port D bit 3 corresponds to INT3 and is used for transmitting data on USART1

        ; Set Port D pin 2 (TXD0) for input and pins 1-0 for whisker input
        ldi mpr, (1<<PD2)|(0<<WskrR)|(0<<WskrL)
        out DDRD, mpr

        ; Set pullup resistors
        ldi mpr, (1<<PD2)|(0<<WskrR)|(0<<WskrL)
        out PORTD, mpr

        ; Initialize Port B for output
        ldi mpr, $FF
        out DDRB, mpr ; Set the DDR register for Port B
        out PORTB, speed ; Set the default output for Port B

    ; Initialize timers
        ; Initialize Timer/Counter 0 for right motor
        LDI     mpr, 0b01110011 ; Activate Fast PWM mode with toggle
        OUT     TCCR0, mpr ; (non-inverting), and set prescalar to 1024
        
        ; Initialize Timer/Counter 2 for left motor
        LDI     mpr, 0b01110011 ; Activate Fast PWM mode with toggle
        OUT     TCCR2, mpr ; (non-inverting), and set prescalar to 1024

        ; Set the compare value for timer/counter 0 and 2
        rcall   UpdateTimers

        ; Initialize Timer/Counter 3 for wait subroutine
        LDI     mpr, 0b00000000
        sts     TCCR3A, mpr
        LDI     mpr, 0b00000100
        sts     TCCR3B, mpr
        ldi     mpr, HIGH($FFFF)
        sts     OCR3AH, mpr
        ldi     mpr, LOW($FFFF)
        sts     OCR3AL, mpr

    ; Initialize USART1
        ldi     mpr, (1<<U2X1) ; Set double data rate
        sts     UCSR1A, mpr

        ; Set baud rate at 2400bps (taking into account double data rate)
        ldi     mpr, high(832) ; Load high byte of 0x0340
        sts     UBRR1H, mpr ; UBRR1H in extended I/O space
        ldi     mpr, low(832) ; Load low byte of 0x0340
        sts     UBRR1L, mpr

        ; Set frame format: 8 data, 2 stop bits
        ldi     mpr, (0<<UMSEL1 | 1<<USBS1 | 1<<UCSZ11 | 1<<UCSZ10)
        sts     UCSR1C, mpr ; UCSR0C in extended I/O space

        ; Enable receiver and receive interrupts
        ldi     mpr, (1<<RXEN1 | 1<<RXCIE1)
        sts     UCSR1B, mpr

    ; Initialize external interrupts
        ; Set the Interrupt Sense Control to level low
        ldi mpr, (0<<ISC21)|(0<<ISC20)|(0<<ISC11)|(0<<ISC10)|(0<<ISC01)|(0<<ISC00)
        sts EICRA, mpr ; Use sts, EICRA in extended I/O space
        ; Set the External Interrupt Mask
        ldi mpr, (1<<INT2)|(1<<INT1)|(1<<INT0)
        out EIMSK, mpr
        ; Turn on interrupts
        sei

;-----------------------------------------------------------
; Main Program
;-----------------------------------------------------------
MAIN:
        rjmp    MAIN

;***********************************************************
;*  Functions and Subroutines
;***********************************************************

USART_Receive:
        push    mpr

        lds     data, UDR1 ; Read data from Receive Data Buffer

        ; Check if it's an ID or a command
        ldi     mpr, 0b10000000
        and     mpr, data
        breq    ProcessID ; If the result is all zeroes, it starts with a 0 and is an ID
        rjmp    ProcessCommand; Otherwise, it's a command

ProcessID:
        cpi     data, BotID
        breq    SetAccept ; If the BotID matches, go into accept mode
        clr     accept ; If it doesn't match, make sure we're NOT in accept mode
        rjmp    USART_Receive_End

SetAccept:
        ldi     accept, 1 ; If it does match, go into accept mode
        rjmp    USART_Receive_End

ProcessCommand:
        cpi     accept, 1
        brne    USART_Receive_End ; If we aren't in accept mode, do nothing
        ; If we are in accept mode, run the command
        lsl     data ; Decode command

CheckUp:
        cpi     data, Up
        brne    CheckDown
        inc     speed
        cpi     speed, 16
        brne    SendCommand
        dec     speed
        rjmp    SendCommand

CheckDown:
        cpi     data, Down
        brne    OtherCommand
        dec     speed
        cpi     speed, 255
        brne    SendCommand
        inc     speed
        rjmp    SendCommand

OtherCommand:
        mov     command, data ; The command is direct motor input
        rjmp    SendCommand

SendCommand:
        mov     mpr, command    ; Upper 4 bits is motor command
        or      mpr, speed      ; Lower 4 bits is LED speed indicator

        rcall   UpdateTimers

        out     PORTB, mpr  ; Send command/speed to port
        clr     accept ; Leave accept mode since we just accepted
        rjmp    USART_Receive_End

USART_Receive_End:
        pop     mpr
        ret


UpdateTimers:
        push    mpr

        mov     mpr, speed
        lsl     mpr
        lsl     mpr
        lsl     mpr
        lsl     mpr
        or      mpr, speed

        out     OCR0, mpr
        out     OCR2, mpr

        pop     mpr
        ret

;----------------------------------------------------------------
; Sub:  HitRight
; Desc: Handles functionality of the TekBot when the right whisker
;       is triggered.
;----------------------------------------------------------------
HitRight:
        push    mpr         ; Save mpr register
        in      mpr, SREG   ; Save program state
        push    mpr         ;

        ; Move Backwards for a second
        ldi     mpr, MovBck ; Load Move Backwards command
        or      mpr, speed
        out     PORTB, mpr  ; Send command to port
        rcall   Wait            ; Call wait function

        ; Turn left for a second
        ldi     mpr, TurnL  ; Load Turn Left Command
        or      mpr, speed
        out     PORTB, mpr  ; Send command to port
        rcall   Wait            ; Call wait function

        ; Resume previous command
        mov     mpr, command
        or      mpr, speed
        out     PORTB, mpr

        pop     mpr     ; Restore program state
        out     SREG, mpr   ;
        pop     mpr     ; Restore mpr
        ret             ; Return from subroutine

;----------------------------------------------------------------
; Sub:  HitLeft
; Desc: Handles functionality of the TekBot when the left whisker
;       is triggered.
;----------------------------------------------------------------
HitLeft:
        push    mpr         ; Save mpr register
        in      mpr, SREG   ; Save program state
        push    mpr         ;

        ; Move Backwards for a second
        ldi     mpr, MovBck ; Load Move Backwards command
        or      mpr, speed
        out     PORTB, mpr  ; Send command to port
        rcall   Wait            ; Call wait function

        ; Turn right for a second
        ldi     mpr, TurnR  ; Load Turn Left Command
        or      mpr, speed
        out     PORTB, mpr  ; Send command to port
        rcall   Wait            ; Call wait function

        ; Resume previous command
        mov     mpr, command
        or      mpr, speed
        out     PORTB, mpr

        pop     mpr     ; Restore program state
        out     SREG, mpr   ;
        pop     mpr     ; Restore mpr
        ret             ; Return from subroutine

; Subroutine to wait for 1 s
Wait:
        LDI B, 1 ; Load loop count = 100
WAIT_10msec:
        LDI mpr, HIGH(0) ; (Re)load value for delay
        sts TCNT3H, mpr
        LDI mpr, LOW(0) ; (Re)load value for delay
        sts TCNT3L, mpr
        ; Wait for TCNT3 to roll over
CHECK:
        lds     mpr, ETIFR ; Read in TIFR
        ANDI    mpr, 0b00000100 ; Check if TOV3 set
        BREQ    CHECK ; Loop if TOV3 not set 
        LDI mpr, 0b00000100 ; Otherwise, Reset TOV3
        sts ETIFR, mpr ; Note - write 1 to reset
        DEC B ; Decrement count
        BRNE WAIT_10msec ; Loop if count not equal to 0
        RET 
\end{verbatim}

\end{document}
